@robot 'roborenderer',
  description: 'Renders a visual representation of a robot or robots'
  local: true

escapeHTML = (html) -> $('<div/>').text(html).html() if html?

me = this


@transmit "add style", "
  .robot {
    width: 50px;
    height: 64px;
    float: left;
    margin: 5px;
  }
  .robot img {
    width: 50px;
    height: 50px;
    border: 1px solid black;
    border-radius: 10px;
  }
  .robot .name {
    width: 50px;
    overflow: hidden;
    text-overflow: ellipsis;
    height: 14px;
    font-size: 12px;
    text-align: center;
  }
"

@listen 'render robots', ({data: {target, robots}}) ->
  target = $(document.getElementById(target))
  els = {}
  for el in target.find('.robot')
    els[el.getAttribute('data-eid')] = el

  console.log "starting with", els
  for robot in robots
    do (robot) ->
      el = $("<div class='robot' data-eid='#{robot.id}' data-parent='#{robot.parent}' title='#{robot.name}'>
        <img src='https://robohash.org/#{encodeURIComponent(robot.name)}?set=set3&size=50x50' />
        <div class='name'>#{escapeHTML robot.name}</div>
      </div>").draggable revert: 'invalid', distance: 15, helper: 'clone', appendTo: 'body'

      el.click ->
        console.log "clicked #{robot.name} with id #{robot.id} and parent id #{robot.parent}"
        me.transmit "robot clicked", robot.id

      oldel = els[robot.id]
      if oldel
        $(oldel).replaceWith el
        delete els[robot.id]
      else
        target.append el

  console.log "removing remaining", els
  for id, el of els
    target[0].removeChild el


# @listen type: 'unrender robots', local: true, ({data: {target, robots}}) ->
#   for id in robots
#     $(target).find("[data-eid='#{id}']").remove()
