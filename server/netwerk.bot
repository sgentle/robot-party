@robot 'netwerk',
  description: "Wraps network-related robots so they can process messages before the messages enter general circulation."
  local: true

messages = {}
registered = incoming: 0, outgoing: 0

maybepass = (id) ->
  if messages[id]?.topass is 0
    messages[id].pass messages[id].msg
    delete messages[id]

# Replace the default distribute behaviour
#
# Don't receive messages from parent normally, instead turn them into
# "outgoing message" messages that can be dealt with by children
#
# Similarly, don't distribute messages up by default. Instead, messages to be
# sent to the parent will be represented by the "incoming message" message.

NETWORK_MESSAGES = 'register incoming,register outgoing,pass,pass with changes,send message,incoming message,outgoing message'.split(',')

createNetworkMessage = (msg) =>
  id = _.randomId()
  messages[id] =
    msg: msg
    topass: registered.outgoing
    pass: (msg) =>
      @transmit "send message", msg
  @transmit type: "outgoing message", id: id, data: msg
  maybepass id

@distribute = (msg, source) ->
  @receive msg unless this is source

  @distributeUp msg, source if @parent isnt source and msg.type not in NETWORK_MESSAGES
  @distributeDown msg, source

  createNetworkMessage msg unless msg.local

@listen "incoming message", ({id, data:msg}) ->
  messages[id] =
    msg: msg
    topass: registered.incoming
    pass: (msg) =>
      @receive msg
      @distributeUp msg, this
      @distributeDown msg, this
  maybepass id


@listen "register incoming", -> registered.incoming++

@listen "register outgoing", -> registered.outgoing++


@listen "pass with changes", ({data:{id, changes}}) ->
  return unless messages[id]

  messages[id].msg[key] = val for key, val of changes
  messages[id].topass--
  maybepass id

@listen "pass", ({data:id}) ->
  return unless messages[id]

  messages[id].topass--
  maybepass id
